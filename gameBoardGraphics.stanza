defpackage GameBoardGraphics :
    import core
    import collections
    import sdl2
    import labyrinth

; some enum
val scale-step = 0.25f

public defstruct GameBoardGraphics :
    size : [Int, Int] with : (setter => set-size)
    window : Long
    win-surface : Long with : (setter => set-win-surface)
    baseBoard : Long
    board : Long
    scalar : Float with : (setter => set-scalar)           ; nominal scale
    scale : Tuple<Float> with : (setter => set-scale)     ; use normalized scale. 1.0 = full source screen, 2.0 quarter source screen zoomed in
    center : Tuple<Float> with : (setter => set-center)   ; use nomralized x, y coordinates. 1.0 = right/bottom, 0.0 = left/top
    ; stock images
    syria : Long
    iranMuslim : Long
    mali : Long
    nigeria_nonMuslim : Long
    nigeria_muslim : Long
    troop : Long
    troopLevel : Long
    troopoffmap : Long

public defn make-GameBoardGraphics () -> GameBoardGraphics :
    val width = 1024
    val height = 768
    val default-scale = 4.0f
    val [window win-surface] = sdl-create-window("Labyrinth", width, height)

    val screen-aspect = to-float(width) / to-float(height)
    val game-board = sdl-load-image( "vassalimages/Labyrinth Map 2016.jpg", win-surface)    ; baseBoard
    val canvas = sdl-create-surface(sdl-surface-size(game-board))
    val [board-w board-h] = sdl-surface-size(game-board)
    val board-aspect = to-float(board-w) / to-float(board-h)
    val adjustment = screen-aspect / board-aspect

    val gbg = GameBoardGraphics( 
        [width, height]
        window
        win-surface
        game-board
        canvas
        default-scale
        [1.0f adjustment]
        [0.5f 0.5f] 
        ; stock images
        sdl-load-image("vassalimages/MapTiles_Syria.jpg", canvas)
        sdl-load-image("vassalimages/MapTiles_Iran(Muslim).jpg", canvas)
        sdl-load-image("vassalimages/MapTiles_Mali.jpg", canvas)
        sdl-load-image("vassalimages/MapTiles_Nigeria(non-Muslim).jpg", canvas)
        sdl-load-image("vassalimages/MapTiles_Nigeria(Muslim).jpg", canvas)
        sdl-load-image("vassalimages/Troops.png", canvas)
        sdl-load-image("vassalimages/TroopsLevel.png", canvas)
        sdl-load-image("vassalimages/MapTiles_Off Map Box.png", canvas)        
        
    )
    ; test-sdl2()
    build-board(gbg)
    show(gbg)
    gbg

defn recalculate (gbg : GameBoardGraphics) :
    val ws = sdl-get-window-surface(window(gbg))
    val size = sdl-surface-size(ws)
    set-size(gbg, size)
    set-win-surface(gbg, ws)

    val screen-aspect = to-float(size[0]) / to-float(size[1])
    val [board-w board-h] = sdl-surface-size(baseBoard(gbg))
    val board-aspect = to-float(board-w) / to-float(board-h)
    val adjustment = screen-aspect / board-aspect
    set-scale(gbg [1.0f adjustment])
    move-center(gbg, 0, 0)

defn overlay (gbg : GameBoardGraphics src-surface : Long top-left : [Int Int]) :
    val [w h] = sdl-surface-size(src-surface)
    val rect = SDL-rect(top-left[0] top-left[1] w h)
    sdl-set-surface-blend-mode(src-surface SDL_BLENDMODE_BLEND)
    sdl-overlay(src-surface false board(gbg) rect)

defn build-board (gbg : GameBoardGraphics) :
    sdl-set-surface-blend-mode(baseBoard(gbg) SDL_BLENDMODE_BLEND)
    sdl-overlay(baseBoard(gbg) false board(gbg) false)

public defn build-board (gbg : GameBoardGraphics, lab : Labyrinth) :
    build-board(gbg)
    var cardsInBox = 0
    if isMuslim(lab "iran") :
        overlay(gbg iranMuslim(gbg) [1780, 826])
    if not isSunni(lab "syria"):
        overlay(gbg syria(gbg) [1393, 800])
    if get(countries(lab) "mali") is-not False :
        overlay(gbg mali(gbg) [267, 1787])
    if get(countries(lab) "nigeria") is-not False :
        if isMuslim(lab "nigeria") :
            overlay(gbg nigeria_muslim(gbg), [567, 1787])
        else:
            overlay(gbg nigeria_nonMuslim(gbg), [567, 1787])
    ; do(
    ;     {drawCountry(gbg, key(_))}
    ;     c(countries(lab))
    ; )
    drawTroopTrack(gbg lab)
    ; drawCellTrack(gbg)
    ; drawMarkers(gbg)
    ; drawAvailablePlots(gbg)
    ; drawReserves(gbg)
    ; drawPhase(gbg)
    ; drawUsedPlots(gbg)
    ; drawDeck(gbg)
    ; drawScores(gbg)
    ; drawGWOT(gbg)
    ; drawPrestige(gbg)
    ; drawFirstPlot(gbg)
    ; drawLapsing(gbg)

public defn show (gbg : GameBoardGraphics lab : Labyrinth) :
    show(gbg)

public defn show (gbg : GameBoardGraphics) :
    val size = sdl-surface-size(baseBoard(gbg))
    val scaled-size = map({to-float(_) / _ / scalar(gbg) } to-list(size) scale(gbg))
    val cs = map({to-int(_ * to-float(_))} to-list $ center(gbg) size)
    
    val src-rect = SDL-rect( 
        to-int(to-float(cs[0]) - scaled-size[0] * 0.5f)
        to-int(to-float(cs[1]) - scaled-size[1] * 0.5f)
        to-int(scaled-size[0])
        to-int(scaled-size[1])
    )
    sdl-clear-surface(win-surface(gbg))
    sdl-overlay(board(gbg), src-rect, win-surface(gbg), false)
    sdl-update(window(gbg))

defn move-center (gbg : GameBoardGraphics, x : Int, y : Int) :
    val size = sdl-surface-size(win-surface(gbg))
    val new-center = map( 
        {_ + _}
        map(
            {_ / to-float(_) / _ / scalar(gbg)} 
            to-list([to-float((- x)) to-float((- y))])
            to-tuple(size)
            scale(gbg)
        )
        center(gbg) 
    )
    val final-center =
        map(
            fn (c s) : max((0.5f / s / scalar(gbg)) min(c (1.0f - 0.5f / s / scalar(gbg))))
            new-center
            scale(gbg)
        ) when scalar(gbg) > 1.0f else
        map(
            {max(0.0f min(_ 1.0f))}
            new-center
        )
    set-center(gbg, to-tuple(final-center))

defn change-scale ( gbg : GameBoardGraphics, change : Float ) :
    val new-scalar = scalar(gbg) + change
    if new-scalar >= 0.5f :
        set-scalar(gbg new-scalar)
        move-center(gbg 0 0)
        show(gbg)

public defn handle-events (gbg : GameBoardGraphics) -> Tuple<Char>|True|False :
    val char-buffer = Vector<Char>()
    label<Tuple<Char>|True|False> return :
        do( fn (e) :
                ;println(e)
                match(e) :
                    (e : SDL-window-event) : 
                        ; println("window event %_" % [e])
                        if event(e) == SDL_WINDOWEVENT_SIZE_CHANGED :
                            val width = data1(e)
                            val height = data2(e)
                            ; println("resized %_ %_" % [width height] )
                            recalculate(gbg)
                            show(gbg)
                        else if event(e) == SDL_WINDOWEVENT_EXPOSED :
                            recalculate(gbg)
                            show(gbg)
                    (e : SDL-multigesture-event) :
                        val d = dDistance(e) * 125.0f
                        if abs(d) >= 0.1f :
                            change-scale(gbg d)

                    (e : SDL-generic-event) : 
                        return(true) when SDL_QUIT == type(e)
                        
                    (e : SDL-mouse-motion-event) :
                        if (state(e) & 1) == 1 :
                            ; println("drag: %_" % [e])
                            move-center(gbg, xrel(e), yrel(e))
                            show(gbg)
                    (e : SDL-keyboard-event) :
                        if state(e) == 0Y and mod(e) == 0 :
                            val c = to-char(sym(e))
                            if c == ',' :
                                change-scale(gbg (- scale-step))
                            else if c == '.' :
                                change-scale(gbg scale-step)
                            else :
                                add(char-buffer, c)
                    (e) : false 
            sdl-event-loop()
        )  
        to-tuple(char-buffer)

defn drawTroopTrack (gbg : GameBoardGraphics lab : Labyrinth) :
    val coord = [3167 153]
    for i in 0 to trooptrack(lab) do:
        val x = (coord[0] - 12 * i) - (i / 5) * 22
        overlay(gbg troop(gbg) [x, coord[1]])
    val troop-level = getTroopLevel(lab)
    val x = 2447 when troop-level == "low" else 2727 when troop-level == "war" else 2993
    overlay(gbg troopLevel(gbg) [x, coord[1] - 4])
    val offmap = offmap(lab)
    if offmap != 0:
        overlay(gbg troopoffmap(gbg) [3000 273])
        val coord = [3100 293]
        val x = coord[0] - (offmap / 2) * 12
        val y = coord[1]
        for i in 0 to offmap do:
            overlay(gbg troop(gbg) [x + 12 * i, y])
